; EOF can be treated the same as <EOL>

<document> ::= <paragraph> <document>
             | <heading> <document>
             | <paragraph>
             | <heading>

<paragraph>         ::= <lines> <paragraph>
                      | <lines>
<lines>             ::= <line> <line> | <line>
<line>              ::= <paragraph-content> <EOL>
<paragraph-content> ::= <emphasis> <paragraph-content>
                      | <strong> <paragraph-content>
                      | <link> <paragraph-content>
                      | <text> <paragraph-content>
                      | <emphasis>
                      | <strong>
                      | <link>
                      | <text>

<emphasis>            ::= "__" <emphasis-content> <emphasis-end-marker>
<emhpasis-end-marker> ::= "__" | <EOL>
<emphasis-content>    ::= <strong> <emphasis-content>
                        | <link> <emphasis-content>
                        | <text> <emphasis-content>
                        | ""
                        ; __ is not permitted

<strong>            ::= "**" <strong-content> <strong-end-marker>
<strong-end-marker> ::= "**" | <EOL>
<strong-content>    ::= <emphasis> <strong-content>
                      | <link> <strong-content> 
                      | <text> <strong-content> 
                      | ""
                      ; ** is not permitted

<heading>                     ::= <unnumbered-heading> | <numbered-heading>
<unnumbered-heading>          ::= <unnumbered-heading-marker> <opt-whitespace> <heading-content> <EOL>
<unnumbered-heading-marker>   ::= "=" <unnumbered-heading-marker> | "="
<numbered-heading>            ::= "##" <opt-numbered-heading-marker> <opt-whitespace> <heading-content> <EOL>
<opt-numbered-heading-marker> ::= "#" <opt-numbered-heading-marker> | ""
<heading-content>             ::= <emphasis> <heading-content>
                                | <strong> <heading-content>
                                | <link> <heading-content>
                                | <text> <heading-content>
                                | ""

<link>                     ::= <link-destination> | <link-text> <link-destination>
<link-destination>         ::= "<" <link-destination-content> <link-end-marker>
<link-end-marker>          ::= ">" | <EOL>
<link-destination-content> ::= <text> <link-destination-content> | "" ; > is not permitted
<link-text>                ::= "<" <link-text-content> ">"
<link-text-content>        ::= <emphasis> <link-text-content>
                             | <strong> <link-text-content>
                             | <text> <link-text-content>
                             | ""
                             ; > is not permitted (but can be escaped)

; number of '`' in the closing delimiter must be greater than or equal to the
; number of '`' used in the opening delimiter
<code-block> ::= <code-block-delimiter> <code-block-metadata> <EOL> <code-block-body> <code-block-delimiter>
<code-block-delimiter>     ::= "``" <opt-code-block-delimiter>
<opt-code-block-delimiter> ::= "`" <opt-code-block-delimiter> | ""
<code-block-metadata>      ::= <opt-whitespace> <code-block-language> <opt-whitespace>
                             | <opt-whitespace> <code-block-language> <opt-whitespace> "," <opt-whitespace> <code-block-filename> <opt-whitespace>
                             | ""
<code-block-language>      ::= <text> ; up to one less '`' permitted as the number of '`' used for opening delimiter
<code-block-filename>      ::= <text> ; up to one less '`' permitted as the number of '`' used for opening delimiter
<code-block-body>          ::= <text> <EOL> <code-block-body>
                             | ""
                             ; up to one less '`' permitted as the number of '`' used for opening delimiter

<list>                           ::= <unordered-list> | <ordered-list>
<unordered-list>                 ::= <unordered-list-item> <unordered-list>
                                   | <unordered-list-item>
<unordered-list-item>            ::= "-" <list-item-content> <list-item-content-continuation>
<ordered-list>                   ::= <ordered-list-item> <ordered-list>
                                   | <ordered-list-item>
<ordered-list-item>              ::= "1." <list-item-content> <list-item-content-continuation>
<list-item-content>              ::= <line> | ""
<list-item-content-continuation> ::= <EOL> " " <opt-whitespace> <list-item-content-continuation>
                                   | <EOL> " " <opt-whitespace> <line> ; consecutive lines are grouped into lines
                                   | <EOL> " " <opt-whitespace> <list>
                                   | ""

<opt-whitespace> ::= " " <opt-whitespace> | ""

;<escape-sequence> ::= "\" <escape-char>
;<escape-char> ::= "\" | "<" | ">" | "_" | "*" | "=" | "#" | "`" | "-"

<text>        ::= <character> <text> | <character>
<character>   ::= <letter> | <digit> | <punctuation>
<letter>      ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit>       ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<punctuation> ::=  "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"
