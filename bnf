<document> ::= <paragraph> <document>
             | <heading> <document>
             | <paragraph>
             | <heading>

<paragraph>         ::= <paragraph-content> <paragraph>
                      | <paragraph-content> <EOL>
<paragraph-content> ::= <emphasis> <paragraph-content>
                      | <strong> <paragraph-content>
                      | <text> <paragraph-content>
                      | <emphasis>
                      | <strong>
                      | <text>

<emphasis>         ::= "__" <emphasis-content> "__"
<emphasis-content> ::= <strong> <emphasis-content>
                     | <text> <emphasis-content>
                     | ""
                     ; __ is not permitted

<strong>         ::= "**" <strong-content> "**"
<strong-content> ::= <emphasis> <strong-content>
                   | <text> <strong-content> 
                   | ""
                   ; ** is not permitted

<heading>                 ::= <heading-marker> <opt-whitespace> <heading-content> <EOL>
<numbered-heading>        :: = "#" <numbered-heading-marker> <opt-whitespace> <heading-content> <EOL>
<heading-marker>          ::= "=" <heading-marker> | "="
<numbered-heading-marker> ::= "#" <numbered-heading-marker> | "#"
<opt-whitespace>          ::= " " <opt-whitespace> | ""
<heading-content>         ::= <emphasis> <heading-content>
                            | <strong> <heading-content>
                            | <text> <heading-content>
                            | ""

<link>                     ::= <link-destination> | <link-text> <link-destination>
<link-destination>         ::= "<" <link-destination-content> ">"
<link-destination-content> ::= <text> <link-destination-content> | "" ; > is not permitted
<link-text>                ::= "<" <link-text-content> ">"
<link-text-content>        ::= <emphasis> <link-text-content>
                             | <strong> <link-text-content>
                             | <text> <link-text-content>
                             | ""
                             ; > is not permitted

; number of '`' in the closing delimiter must be greater than or equal to the
; number of '`' used in the opening delimiter
<code-block> ::= <code-block-delimiter> <code-block-metadata> <EOL> <code-block-body> <code-block-delimiter>
<code-block-delimiter>            ::= "``" <code-block-delimiter-additional>
<code-block-delimiter-additional> ::= "`" <code-block-delimiter-additional> | ""
<code-block-metadata>             ::= <opt-whitespace> <code-block-language> <opt-whitespace>
                                    | <opt-whitespace> <code-block-language> <opt-whitespace> "," <opt-whitespace> <code-block-filename> <opt-whitespace>
                                    | ""
<code-block-language>             ::= <text> ; up to one less '`' permitted as the number of '`' used for opening delimiter
<code-block-filename>             ::= <text> ; up to one less '`' permitted as the number of '`' used for opening delimiter
<code-block-body>                 ::= <text> <EOL> <code-block-body>
                                    | ""
                                    ; up to one less '`' permitted as the number of '`' used for opening delimiter

<text>        ::= <character> <text> | <character>
<character>   ::= <letter> | <digit> | <punctuation>
<letter>      ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit>       ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<punctuation> ::=  "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"
